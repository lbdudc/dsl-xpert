<root> ::= <createStatement>+
<createStatement> ::= "CREATE " (<createProduct> | <createRange> | <createDimension> | <createSensor>)
<createProduct> ::= "PRODUCT " <identifier> " USING " <srid> ";"
<createRange> ::= "RANGE " <identifier> " (" <rangeProperty> ("," <rangeProperty>)* ")" ";"
<rangeProperty> ::= <rangeNumber> (" TO " <rangeNumber>)? " AS " <text> (" COLOR " <hexColor>)? | " DEFAULT " "AS " <text> (" COLOR " <hexColor>)?
<rangeNumber> ::= <identifier> | <floatNumber> | <intNumber> | "INFINITY" | "-INFINITY"
<createDimension> ::= <createSpatialDimension> | <createCategoricalDimension>
<createSpatialDimension> ::= "SPATIAL DIMENSION " <identifier> " (" "geometry:" (<type> | "Geometry") ")" <createDimensionProperties> <createParentDimension>? ";"
<createCategoricalDimension> ::= "CATEGORICAL DIMENSION " <identifier> " (" "FIELD:" <identifier> ")" ";"
<createParentDimension> ::= "WITH PARENT" "(" <identifier> ("," <identifier>)* ")"
<createDimensionProperties> ::= " WITH PROPERTIES " "(" <dimPropertyDefinition> ("," <dimPropertyDefinition>)* ")"
<dimPropertyDefinition> ::= <identifier> <type>
<createSensor> ::= "SENSOR " <identifier> " (" "interval: " <intNumber> "," "datasource: " <dataSource> "," "geometry: " <type> ")" <createSensorProperties> <createSensorMeasurementData> (<addSpatialDimensionToSensor>)* (<addCategoricalDimensionToSensor>)? (<addBBXToSensor>)? ";"
<createSensorProperties> ::= " WITH PROPERTIES " "(" <sensorPropertyDefinition> ("," <sensorPropertyDefinition>)* ")"
<sensorPropertyDefinition> ::= <identifier> <type>
<createSensorMeasurementData> ::= " WITH MEASUREMENT DATA " "(" <createMeasurementProperty> ("," <createMeasurementProperty>)* ")"
<createMeasurementProperty> ::= <identifier> <type> (" UNITS " <text>)? (" ICON " <text>)? (" RANGE " <identifier>)?
<addSpatialDimensionToSensor> ::= " WITH SPATIAL DIMENSIONS " <identifier> "(" <identifier> ("," <identifier>)* ")"
<addCategoricalDimensionToSensor> ::= " WITH CATEGORICAL DIMENSIONS " "(" <identifier> (" RANGE " <identifier>)? ("," <identifier> (" RANGE " <identifier>)?)* ")"
<addBBXToSensor> ::= " WITH BBOX (" ("[")? <coordinate> ("]")? ", " <intNumber> ")"
<srid> ::= <intNumber>
<identifier> ::= <text>
<char> ::= [a-z] | [A-Z] | [0-9] | "-" | "_"
<text> ::= <char> | <char> <text>
<quotedText> ::= "\"" <text> "\""
<type> ::= "Long" | "Boolean" | "Float" | "Integer" | "Double" | "Localdate" | "String" | "Datetime" | "Linestring" | "Multilinestring" | "Polygon" | "Multipolygon" | "Point" | "Multipoint"
<dataSource> ::= "postgres" | "elasticsearch"
<hexColor> ::= "#" <hexDigit> <hexDigit> <hexDigit> <hexDigit> <hexDigit> <hexDigit>
<hexDigit> ::= <digit> | [a-f]
<digit> ::= [0-9]
<digits> ::= <digit>+
<intNumber> ::= <digits>
<floatNumber> ::= (<digits>? ".")? <digits>
<negativeFloat> ::= ("-")? <floatNumber>
<coordinate> ::= <negativeFloat> ", " <negativeFloat>
